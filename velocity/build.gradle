import org.apache.tools.ant.filters.ReplaceTokens

plugins {
    id 'id.luckynetwork.lyrams.lyralibs.java-conventions'
}

configurations {
    otherProjects
}

configurations {
    extraLibs
}

def pluginName = 'LyraLibs-Velocity'
def code = 0

dependencies {
    compileOnly project(":core")

    annotationProcessor 'org.projectlombok:lombok:1.18.20'
    annotationProcessor 'com.velocitypowered:velocity-api:3.0.1'

    //mongodb
    extraLibs 'org.mongodb:mongo-java-driver:3.12.10'
    // jedis
    extraLibs 'redis.clients:jedis:3.7.0'
    // gson
    extraLibs 'com.google.code.gson:gson:2.8.6'

    // lombok
    compileOnly 'org.projectlombok:lombok:1.18.20'
    // jetbrains annotations
    compileOnly 'org.jetbrains:annotations:20.1.0'
    // velocity
    compileOnly 'com.velocitypowered:velocity-api:3.0.1'

    configurations.compileOnly.extendsFrom(configurations.extraLibs)
    configurations.compileOnly.extendsFrom(configurations.otherProjects)
}

task updateBuildNumber {
    def versionPropsFile = file 'version.properties'
    Properties versionProps = new Properties()
    versionProps.load(new FileInputStream(versionPropsFile))

    code = versionProps['VERSION_CODE'].toInteger() + 1

    versionProps['VERSION_CODE'] = code.toString()
    versionProps.store(versionPropsFile.newWriter(), null)
}

jar {
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE

    archiveFileName = "LyraLibs-Velocity b" + code.toString() + ".jar"

    from {
        configurations.extraLibs.collect { it.isDirectory() ? it : zipTree(it) }
    }
    from {
        configurations.otherProjects.collect { it.isDirectory() ? it : zipTree(it) }
    }
}

task jarLight(type: Jar) {
    from sourceSets.main.output

    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
    archiveFileName = "LyraLibs-Velocity-Light b" + code.toString() + ".jar"

    from {
        configurations.otherProjects.collect { it.isDirectory() ? it : zipTree(it) }
    }
}

processResources {
    filter ReplaceTokens, tokens: [
            "version"  : project.version.toString(),
            "mainClass": project.group + ".$pluginName",
            "name"     : pluginName,
            "number"   : code.toString()
    ]
}

assemble.dependsOn(jarLight)
